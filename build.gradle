plugins {
    id "nebula.ospackage" version "4.6.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'nebula.ospackage-base'

sourceCompatibility = 1.7
version = '0.1'

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
    test {
        java {
            srcDirs = ["test"]
        }
    }
}

repositories
{
    mavenCentral()

    flatDir {
       dirs 'libs'
    }
}

dependencies
{
    // compile name: 'protobuf-core'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.2.0'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'
    compile group: 'org.apache.commons', name: 'commons-pool2', version: '2.4.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'org.apache.derby', name: 'derby',       version: '10.12.1.1'
    compile group: 'org.apache.derby', name: 'derbyclient', version: '10.12.1.1'
    compile group: 'org.apache.derby', name: 'derbynet',    version: '10.12.1.1'
//    compile group: 'org.apache.derby', name: 'derbytools',  version: '10.12.1.1'

    testCompile group: 'junit', name: 'junit', version: '4.+'
    // testCompile group: 'org.mockito', name: 'mockito', version: '2.7.19'
    testCompile group: 'org.powermock', name: 'powermock-mockito-release-full', version: '1.6.4'
}

processResources {
    from('sql-src/drbd-init-derby.sql') { into '/resource' }
}

mainClassName = 'com.linbit.linstor.core.Controller'
jar {
  manifest {
    attributes(
      'Main-Class': mainClassName
    )
  }
}

task ControllerStartup(type: CreateStartScripts) {
    mainClassName = 'com.linbit.linstor.core.Controller'
    applicationName = "Controller"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
    doLast {
        delete windowsScript
    }
}


task SatelliteStartup(type: CreateStartScripts) {
    mainClassName = 'com.linbit.linstor.core.Satellite'
    applicationName = "Satellite"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
    doLast {
        delete windowsScript
    }
}

task RecreateDbStartup(type: CreateStartScripts) {
    mainClassName = 'com.linbit.linstor.core.RecreateDb'
    applicationName = "RecreateDb"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
    doLast {
        delete windowsScript
    }
}

applicationDistribution.into("bin") {
    from(ControllerStartup)
    from(SatelliteStartup)
    from(RecreateDbStartup)
    fileMode = 0755
}

task showMeCache << {
  configurations.compile.each { println it }
}

task buildTestCommands(type:Exec) {
  workingDir 'test-support'

  commandLine 'make'

  //store the output instead of printing to the console:
  standardOutput = new ByteArrayOutputStream()

  //extension method buildTestCommands.output() can be used to obtain the output:
  ext.output = {
    return standardOutput.toString()
  }
}

task cleanTestCommands(type:Exec) {
  workingDir 'test-support'

  commandLine 'make', 'clean'

  //store the output instead of printing to the console:
  standardOutput = new ByteArrayOutputStream()

  //extension method buildTestCommands.output() can be used to obtain the output:
  ext.output = {
    return standardOutput.toString()
  }
}

compileTestJava {
  dependsOn buildTestCommands
}

clean {
  dependsOn cleanTestCommands
}

ospackage {
    packageName 'linstor-server'
    release '1'
    os = LINUX
    user 'root'

    requires('drbd-utils', '9.2.0', GREATER | EQUAL)

    postInstall file('scripts/postinstall.sh')

    from('scripts/linstor-controller.service') {
        into '/lib/systemd/system'
        fileMode 0644
    }
    from('scripts/linstor-satellite.service') {
        into '/lib/systemd/system'
        fileMode 0644
    }

    into '/opt/linstor-server'
    from(jar.outputs.files) {
        into 'lib'
    }
    from(configurations.runtime) {
        into 'lib'
    }
    from(ControllerStartup) {
        into 'bin'
        fileMode 0550
    }
    from(SatelliteStartup) {
        into 'bin'
        fileMode 0550
    }
    from(RecreateDbStartup) {
        into 'bin'
        fileMode 0550
    }
}

// we might even split these to generate separate controller/satellite packages
buildRpm {
    requires('java-1.7.0-openjdk')
    requires('lvm2')
}

buildDeb {
    requires('openjdk-7-jre')
    requires('thin-provisioning-tools')
}
