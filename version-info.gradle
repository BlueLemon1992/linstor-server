import java.text.SimpleDateFormat

task versionInfo(type: VersionInfoTask) {
    outputDir = file("$projectDir/generated-resources")
}

class VersionInfoTask extends DefaultTask {
    @OutputDirectory
    File outputDir

    VersionInfoTask() {
        outputs.upToDateWhen { false }
    }

    def getGitHash() {
        def stdOut = new ByteArrayOutputStream()
        project.exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdOut
        }
        return stdOut.toString().trim()
    }

    @TaskAction
    def generateVersionInfo() {
        File file = new File(outputDir, "version-info.properties")
        file.delete()
        file << "version=${project.version}\n"
        file << "git.commit.id=${getGitHash()}\n"
        file << "build.time=${new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())}\n"
    }
}

processResources.dependsOn versionInfo
